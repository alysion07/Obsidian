
## Directional Lighting

#조명 #lighting #내적

조명 효과를 구현하는 방법은 여러가지가 있지만 가장 간단한 것은 **Directional Lighting**(방향성 조명 효과)이다.  빛이 **한 방향**으로부터 **일정**하게 들어온다고 가정한다. 태양은 매우 멀고 거대하기 때문에 물체의 표면에 모두 **평행**하게 도달한다고 생각할 수 있다.

**Directional Lighting** 효과를 계산하는 방법은 물체의 표면이 바라보는 벡터와 빛의 진행 벡터의 내적을 구하면 두 방향간의 코사인 각도를 계산할 수 있다.

![[스크린샷 2024-08-06 103151.png |center]]

두 벡터가 완전이 **같은** 경우 내적값은 **1**이 되고, **정 반대**의 경우는 **-1**이 된다.

![[스크린샷 2024-08-06 103632.png | center]]
<center> light direction에 따른 내적값 결과  </center>
### 요약 
색상에 내적값을 곱하면 조명 효과가 **적용된 색상값**을 얻을 수 있다.


## 법선(Normal)에 대한 소개

3D 그래픽스 분야에서 법선(normal)이란 **표면이 바라보는 방향**을 나타내는 **단위 벡터**(unit vector)입니다.

## Shader 적용

```cpp title:'fragment shader' hl:4,20,26
#version 300 es
precision highp float;
// Passed in and varied from the vertex shader.
in vec3 v_normal;

uniform vec3 u_reverseLightDirection;
uniform vec4 u_color;

// we need to declare an output for the fragment shader
out vec4 outColor;

void main() {
  // because v_normal is a varying it's interpolated
  // so it will not be a uint vector. Normalizing it
  // will make it a unit vector again
  vec3 normal = normalize(v_normal);

  // compute the light by taking the dot product
  // of the normal to the light's reverse direction
  float light = dot(normal, u_reverseLightDirection);

  outColor = u_color;

  // Lets multiply just the color portion (not the alpha)
  // by the light
  outColor.rgb *= light;
}
```

`normalize`: 입력한 값을 Unit Vector로 변환한다.

## Transpose Matrix

오브젝트의 회전하더라도 정상적인 조명 효과 적용을 위해서 `normal`에 `world`를 곱해주는데, 만약 `world`에 `scale`이 포함되어 있는 경우, 계산 된 `normal`의 값이 잘못 도출된다(직교 X).  이때 **Transpose Matrix**(전치 행렬)을 사용하여 해결할 수 있다. 아래 이미지 중 **파란색 오브젝트**가 전치행렬을 사용하여 보정한 경우이다.
 ![[스크린샷 2024-08-06 110634.png| center]]
normal에 Transpose Matrix 

```cpp title:'vertex shader' hl:8,9, ar:19
#version 300 es

// an attribute is an input (in) to a vertex shader.
// It will receive data from a buffer
in vec4 a_position;
in vec3 a_normal;

uniform mat4 u_worldViewProjection;
uniform mat4 u_worldInverseTranspose;

// varying to pass the normal to the fragment shader
out vec3 v_normal;

// all shaders have a main function
void main() {
  // Multiply the position by the matrix.
  gl_Position = u_worldViewProjection * a_position;
  // orient the normals and pass to the fragment shader
  v_normal = mat3(u_worldInverseTranspose) * a_normal;
}
```

----
## Summary

표면의 normal과  Light의 방향 백터의 내적을 계산하여 Lighting 효과를 구현

- `Near`, `Far`, `FOV`, `aspect` 를 사용하여 `projection matrix` 계산
- `camera matrix` 계산
- `camera matrix`의 역행렬을 사용하여 `view matrix` 계산
- `projection matrix`에 `view matrix`를 곱해 원근감이 적용된 **view**(사용자가 보게 될 장면)를 계산

- `move`, `rotation`, `scale`이 적용된 `world matrix` 생성
- **view**에 `world matrix`를 적용해 **world** **coordi**를 반영해준다.
- **normal**에도 `world matrix` 를 사용해 **world** **coordi** 를 반영한 뒤, Lighting 효과 구현
#### InverseTranspose matrix 

`world matrix`에 `scale`이 적용된 경우 단순히 `world matrix`를 **normal**에 적용하면 **normal이 표면에 직교하지 않는 현상이 발생**한다. 이를 해결하기 위해 world matrix의 **inverse * transpose** 행렬을 사용해 normal을 계산한 후, Lighting 효과를 적용한다.


---

## mat3(u_worldInverseTranspose) * a_normal의 대안

첫째로는 `u_worldInverseTranspose` 전체 행렬을 다른 용도로도 사용할 수 있기 때문에 `u_worldInverseTranspose`를 `mat4`로 넘기게 되면 그러한 다른 용도로도 활용할 수 있습니다.
또다른 이유로는 우리의 자바스크립트 행렬 함수들이 모두 4x4 행렬을 만든다는 것입니다. 3x3행렬을 위한 기능들을 새로 만들거나 4x4행렬을 3x3행렬로 변환하는 기능을 구현하는 것은 특별한 이유가 있지 않다면 할 필요가 없습니다.